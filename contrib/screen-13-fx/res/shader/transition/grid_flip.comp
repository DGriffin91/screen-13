#version 460 core

// Adapted from:
// https://github.com/gl-transitions/gl-transitions/blob/master/transitions/
// b62319705bc54ab2597c36a3694c2476f8f52495
//
// License: MIT
// Author:TimDonselaar
// ported by gre from https://gist.github.com/TimDonselaar/9bcd1c4b5934ba60087bdb55c2ea92e5

#include "_defs.glsl"

layout(push_constant) uniform PushConstants {
    layout(offset = 0) float progress;
    layout(offset = 4) float pause; // = 0.1
    layout(offset = 8) ivec2 size; // = ivec2(4)
    layout(offset = 16) vec4 bgcolor; // = vec4(0.0, 0.0, 0.0, 1.0)
    layout(offset = 32) float dividerWidth; // = 0.05
    layout(offset = 36) float randomness; // = 0.1
} push_const;

float rand (vec2 co) {
  return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float getDelta(vec2 p) {
  vec2 rectanglePos = floor(vec2(push_const.size) * p);
  vec2 rectangleSize = vec2(1.0 / vec2(push_const.size).x, 1.0 / vec2(push_const.size).y);
  float top = rectangleSize.y * (rectanglePos.y + 1.0);
  float bottom = rectangleSize.y * rectanglePos.y;
  float left = rectangleSize.x * rectanglePos.x;
  float right = rectangleSize.x * (rectanglePos.x + 1.0);
  float minX = min(abs(p.x - left), abs(p.x - right));
  float minY = min(abs(p.y - top), abs(p.y - bottom));
  return min(minX, minY);
}

float getDividerSize() {
  vec2 rectangleSize = vec2(1.0 / vec2(push_const.size).x, 1.0 / vec2(push_const.size).y);
  return min(rectangleSize.x, rectangleSize.y) * push_const.dividerWidth;
}

vec4 transition(vec2 p) {
  if(push_const.progress < push_const.pause) {
    float currentProg = push_const.progress / push_const.pause;
    float a = 1.0;
    if(getDelta(p) < getDividerSize()) {
      a = 1.0 - currentProg;
    }
    return mix(push_const.bgcolor, getFromColor(p), a);
  }
  else if(push_const.progress < 1.0 - push_const.pause){
    if(getDelta(p) < getDividerSize()) {
      return push_const.bgcolor;
    } else {
      float currentProg = (push_const.progress - push_const.pause) / (1.0 - push_const.pause * 2.0);
      vec2 q = p;
      vec2 rectanglePos = floor(vec2(push_const.size) * q);
      
      float r = rand(rectanglePos) - push_const.randomness;
      float cp = smoothstep(0.0, 1.0 - r, currentProg);
    
      float rectangleSize = 1.0 / vec2(push_const.size).x;
      float delta = rectanglePos.x * rectangleSize;
      float offset = rectangleSize / 2.0 + delta;
      
      p.x = (p.x - offset)/abs(cp - 0.5)*0.5 + offset;
      vec4 a = getFromColor(p);
      vec4 b = getToColor(p);
      
      float s = step(abs(vec2(push_const.size).x * (q.x - delta) - 0.5), abs(cp - 0.5));
      return mix(push_const.bgcolor, mix(b, a, step(cp, 0.5)), s);
    }
  }
  else {
    float currentProg = (push_const.progress - 1.0 + push_const.pause) / push_const.pause;
    float a = 1.0;
    if(getDelta(p) < getDividerSize()) {
      a = currentProg;
    }
    return mix(push_const.bgcolor, getToColor(p), a);
  }
}

#include "_main.glsl"
